#labels SL,Format,Syntax,Highlighting
=Подсветка синтаксиса словарей SL в VIM=

Для добавления простенькой подсветки синтаксиса (но, тем не менее, весьма полезной) словарей формата SL нужно создать файл */home/user/.vim/syntax/sldict.vim* следующего содержания:
{{{
""""""""""""""""""""""""""""""""""""""""""""""""""
" Language:	SL-Dict                          "
" Maintainer:	Devaev Maxim <mdevaev@gmail.com> "
" Updated:	2009-04-09                       "
""""""""""""""""""""""""""""""""""""""""""""""""""


if version < 600
  syntax clear
elseif exists("b:current_syntax")
  finish
endif


syn region	sl_dict_bold_region		start=+[uU]\=\\\[+ end=+\\\]+ contains=sl_dict_escapes
syn region	sl_dict_italic_region		start=+[uU]\=\\(+ end=+\\)+ contains=sl_dict_escapes
syn region	sl_dict_official_region		start=+[uU]\=\\<+ end=+\\>+ contains=sl_dict_escapes
syn region	sl_dict_underlined_region	start=+[uU]\=\\_+ end=+\\_+ contains=sl_dict_escapes
syn region	sl_dict_sound_region		start=+[uU]\=\\s+ end=+\\s+ contains=sl_dict_escapes

syn match	sl_dict_comment			"#.*$"
syn match	sl_dict_escapes			+\\[nt\\]+

if version >= 508 || !exists("did_sldict_syn_inits")
  if version <= 508
    let did_sldict_syn_inits = 1
    command -nargs=+ HiLink hi link <args>
  else
    command -nargs=+ HiLink hi def link <args>
  endif

  HiLink sl_dict_bold_region		Statement
  HiLink sl_dict_italic_region		Constant
  HiLink sl_dict_official_region	Type
  HiLink sl_dict_underlined_region	Underlined
  HiLink sl_dict_sound_region		Identifier
  HiLink sl_dict_comment		Comment
  HiLink sl_dict_escapes		Special
endif


let b:current_syntax = "sldict"
}}}

После этого для ее включения можно воспользоваться командой редактора *set syntax=sldict* или добавить в файл */home/user/.vimrc* такую строку:
{{{
au BufRead,BufNewFile *.??-??  set filetype=sldict
}}}